apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'java'
apply from: 'external-dependencies.gradle'
apply from: 'idea-config.gradle'

group 'uk.gov'

// Use Java 8 by default
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

// UTF-8 should be standard by now. So use it!
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.github.ben-manes:gradle-versions-plugin:0.13.0"
        classpath 'org.owasp:dependency-check-gradle:1.4.4.1'
    }
}

apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'org.owasp.dependencycheck'

dependencyCheck {
    failBuildOnCVSS = 0
    suppressionFile = 'dependency-check-suppressions.xml'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.6'
}

repositories {
    mavenCentral()
    maven { url 'https://dl.bintray.com/alphagov/maven' }
    maven { url "https://dl.bintray.com/reliability-engineering-gds/gds_metrics_dropwizard" }
}

project.ext {
    dropwizardConfig = './src/dist/config.yaml'
}

//noinspection GroovyAssignabilityCheck
configurations {
    compile.exclude module: 'slf4j-log4j12'
    compile.exclude module: 'slf4j-simple' // dropwizard uses logback, not slf4j-simple
}

dependencies {
    compile dropwizard
    compile dropwizardLogstash
    compile dropwizardPrometheus

    testCompile junit, mockito, dropwizardTest
}

jar.baseName = "dropwizard-example"

mainClassName = 'uk.gov.ExampleApplication'

run {
    args 'server', dropwizardConfig
}
